#!/usr/bin/env perl

# LICENSE: GPLv3+

use 5.014;
use warnings;
use strict;
use File::Temp qw/tempfile/;
use utf8;
use Test::More;
if (!eval 'use YAML::XS qw/LoadFile/; 1;') {
    die "cannot find the YAML::XS Perl module.\n" .
	"Try '\$ apt-get install libyaml-libyaml-perl'.\n";
}

use constant GET_OUTLINE => "../bookmark-djvu-extract";
use constant SET_OUTLINE => "../bookmark-djvu";

# create empty djvu
my $djvu = create_djvu (8);

# ################################# #
# compare files after set and get:  #
# ################################# #

system_ok (SET_OUTLINE . " -s $djvu input1");

(undef, my $outline) = tempfile (UNLINK => 1);
system_ok (GET_OUTLINE . " -s $djvu -o $outline");

# compare
files_ok ($outline, "expected1");
unlink ($outline);

# default filenames

$djvu = create_djvu (8);

$outline = ($djvu =~ s/\.djvu$/.bm/r);
say "outline: $outline";
    
system_ok ("cp input1 $outline");
# compare files after set and get:
 
system_ok (SET_OUTLINE . " -s $djvu");

unlink ($outline);

system_ok (GET_OUTLINE . " -s $djvu");

# compare
files_ok ($outline, "expected1");
unlink ($outline);

# #### #
# YAML #
# #### #

$djvu = create_djvu (5);

system_ok (SET_OUTLINE . " $djvu input1.yaml");

(undef, $outline) = tempfile (UNLINK => 1);
system_ok (GET_OUTLINE . " $djvu -o $outline");
yaml_ok ("input1.yaml", $outline);

# default filenames

$djvu = create_djvu (5);

$outline = ($djvu =~ s/\.djvu$/.outline/r);
say "outline: $outline";
    
system_ok ("cp input1.yaml $outline");

# compare files after set and get:
 
system_ok (SET_OUTLINE . " $djvu");

unlink ($outline);

system_ok (GET_OUTLINE . " $djvu");

# compare
yaml_ok ("input1.yaml", $outline);
unlink ($outline);

# ############### #
# remove outlines #
# ############### #

$djvu = create_djvu (5);
system_ok (SET_OUTLINE . " $djvu input1.yaml");

# empty input file
system_ok (SET_OUTLINE . " $djvu /dev/null");

system_is (GET_OUTLINE . " $djvu -o /dev/null", 1);

#simple format

$djvu = create_djvu (5);
system_ok (SET_OUTLINE . " $djvu input1.yaml");

# empty input file
system_ok (SET_OUTLINE . " $djvu -s /dev/null");

system_is (GET_OUTLINE . " $djvu -o /dev/null", 1);

done_testing ();

sub yaml_ok {
    my ($file1, $file2) = @_;

    is_deeply (LoadFile ($file1), LoadFile ($file2),
	       "yaml_ok: file1=$file1, file2=$file2");
}

sub system_ok {
    my ($command) = @_ ;
    
    ok (system ($command) == 0, "command: $command");
}

sub system_is {
    my ($command, $exit_status) = @_;
    
    system ($command);
    
    # see 'perldoc -f system'
    ok ($? != -1
	&& !($? & 127)
	&& $? >> 8 == $exit_status,
	"exit status of '$command' is '$exit_status'"); 
}

sub files_ok {
    system_ok ("diff -C 3 $_[0] $_[1]");
}

# arguments: page count, suffix
# returns filename
sub create_djvu {
    my ($page_count, $suffix) = @_;

    $suffix or $suffix = ".djvu";
    
    $page_count > 0 or $page_count = 1;
    
    # create one-page djvu file
    (undef, my $filename) = tempfile (UNLINK => 1, SUFFIX => $suffix);
    system_wrapper ("djvumake $filename INFO=100,100,100");

    my $page;
    if ($page_count > 1) {
	(undef, $page) = tempfile (UNLINK => 1, SUFFIX => ".djvu");
	system_wrapper ("djvumake $page INFO=100,100,100");
    }
    
    for (2..$page_count) {
	system_wrapper ("djvm -i $filename $page");
    }

    return $filename;
}

sub system_wrapper {
    my ($command) = @_;
    
    system ($command) == 0
	or die "command '$command' failed with status ", $? >> 8, "\n";
}
