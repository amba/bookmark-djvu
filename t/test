#!/usr/bin/env perl

# LICENSE: GPLv3+

use 5.014;
use warnings;
use strict;
use File::Temp qw/tempfile/;
use utf8;
use Test::More;

# create empty djvu
my $djvu = create_djvu (8, ".djvu");

# ################################# #
# compare files after set and get:  #
# ################################# #

system_ok ("../bookmark-djvu $djvu input1");

(undef, my $outline) = tempfile (UNLINK => 1);
system_ok ("../bookmark-djvu-extract $djvu -o $outline");

# compare
system_ok ("diff -C 3 $outline expected1");

# ######################## #
# check default filenames  #
# ######################## #

$djvu = create_djvu (8, ".djvu");

$outline = ($djvu =~ s/\.djvu$/.bm/r);
say "outline: $outline";
    
system_ok ("cp input1 $outline");
# compare files after set and get:
 
system_ok ("../bookmark-djvu $djvu");

unlink ($outline);

system_ok ("../bookmark-djvu-extract $djvu");

# compare
system_ok ("diff -C 3 $outline expected1");

done_testing ();

sub system_ok {
    my $command = shift;
    is (system ($command), 0, "command: $command");
}

# arguments: page count, suffix
# returns filename
sub create_djvu {
    my $page_count = shift;
    my $suffix = shift;
    
    $page_count > 0 or $page_count = 1;
    
    # create one-page djvu file
    (undef, my $filename) = tempfile (UNLINK => 1, SUFFIX => $suffix);
    system_wrapper ("djvumake $filename INFO=100,100,100");

    my $page;
    if ($page_count > 1) {
	(undef, $page) = tempfile (UNLINK => 1, SUFFIX => ".djvu");
	system_wrapper ("djvumake $page INFO=100,100,100");
    }
    
    for (2..$page_count) {
	system_wrapper ("djvm -i $filename $page");
    }

    return $filename;
}

sub system_wrapper {
    my $command = shift;
    system ($command) == 0
	or die "command '$command' failed with status ", $? >> 8, "\n";
}
